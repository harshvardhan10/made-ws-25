pipeline TreePipeline {

    TreeExtractor
        -> TreeTextFileInterpreter
        -> TreeCSVInterpreter
        -> BaumartDeutschColumnDeleter 
        -> TreeTableInterpreter
        -> TreeLoader;

    block TreeExtractor oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
    }

    block TreeTextFileInterpreter oftype TextFileInterpreter {}
    
    block TreeCSVInterpreter oftype CSVInterpreter {
        delimiter: ';';
    }

    // delete baumart_deutsch column
    block BaumartDeutschColumnDeleter oftype ColumnDeleter {
        delete: [column E];
    }
    
    block TreeTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "lfd_nr" oftype integer,
            "stadtteil" oftype ValidStadtteil,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "id" oftype ValidGeoPoint,
            "baumfamilie" oftype text 
        ];
    }

    block TreeLoader oftype SQLiteLoader {
        table: 'trees';
        file: './trees.sqlite';
    }
}

valuetype ValidStadtteil oftype text {
    constraints : [
        StartsWithVogelsang
    ]; 
}

constraint StartsWithVogelsang on text :
    value matches /^Vogelsang.*/;

valuetype ValidGeoPoint oftype text {
    constraints : [
        GeoCoordinate
    ];
}

constraint GeoCoordinate on text :
    value matches /^\d{1,3}\.\d+, \d{1,3}\.\d+$/;
